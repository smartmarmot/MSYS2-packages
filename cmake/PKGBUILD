# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>

pkgname=cmake
pkgver=3.5.2
pkgrel=1
pkgdesc="A cross-platform open-source make system"
arch=('i686' 'x86_64')
url="https://www.cmake.org/"
license=("MIT")
makedepends=("gcc" "pkg-config"
             jsoncpp
             libcurl-devel
             libexpat-devel
             libarchive-devel
             ncurses-devel
             zlib-devel)
depends=("gcc-libs"
         "libcurl"
         "libexpat"
         "libarchive"
         "ncurses"
         "pkg-config"
         "zlib")
options=('staticlibs' 'strip')
source=("https://www.cmake.org/files/v3.5/${pkgname}-${pkgver}.tar.gz"
        "cmake-3.5.2-msys.patch"
        "disable-curses-for-msys.patch"
        "disable-response-files-for-msys.patch"
        "3.5.2-case-sensitivity.patch"
        "3.5.2-cpuinfo.patch"
        "3.5.2-cygwin-paths.patch")
sha256sums=('92d8410d3d981bb881dfff2aed466da55a58d34c7390d50449aa59b32bb5e62a'
            'ff62957b203f001dc5ae9f628a2ff4276029d976c3f6b5e53a806f9074ab5d1e'
            'b7e398e70b97088f88a0688e8a0794d6780d3cdb91075388a30908dc4b405eb7'
            '98dca846de0ca7b71884e26678317f85e78e01862d58a29ce923c835ca7d614f'
            '4f312510b45774ef299982ea112efbae4ef89cc680a7e4a14f2d1cf619b9749c'
            '6bfd0a843378172883095c409cd4218f755efcbe7777889ade07ef126db6941b'
            '320a2f88d5a3c1fe0a63b992ce45cf228e9a121406b0ebf7654bc8c25d1ad79d')

prepare() {
  cd ${pkgname}-${pkgver}
  rm -f Modules/CPackMsys.cmake \
    Modules/FindMsys.cmake \
    Modules/Platform/MSYS-GNU.cmake \
    Modules/Platform/MSYS.cmake \
    Modules/Platform/MSYS-CXX.cmake \
    Modules/Platform/MSYS-GNU-C.cmake \
    Modules/Platform/MSYS-GNU-CXX.cmake \
    Modules/Platform/MSYS-GNU-Fortran.cmake \
    Modules/Platform/MSYS-windres.cmake || true
  patch -p1 -i "${srcdir}/cmake-3.5.2-msys.patch"
  patch -p1 -i "${srcdir}/disable-curses-for-msys.patch"
  patch -p1 -i "${srcdir}/disable-response-files-for-msys.patch"
  patch -p1 -i "${srcdir}/3.5.2-case-sensitivity.patch"
  patch -p1 -i "${srcdir}/3.5.2-cpuinfo.patch"
  patch -p1 -i "${srcdir}/3.5.2-cygwin-paths.patch"
}

build() {
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  MSYSTEM=MSYS "${srcdir}"/${pkgname}-${pkgver}/configure \
    --prefix=/usr \
    --system-libs \
    --no-qt-gui \
    --parallel=3 \
    --mandir=share \
    --docdir=share/doc/cmake
#--no-system-expat
#--no-system-jsoncpp
  plain "Start building..."
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR=$pkgdir install

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/Copyright.txt \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
